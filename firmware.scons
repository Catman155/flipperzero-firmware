Import("env")

import os

env.Append(
    LIB_DIST_DIR="${BUILD_DIR}/lib",
)

env.Append(
    LIBPATH=[
        "${LIB_DIST_DIR}",
    ],
    CPPDEFINES=[
        ("TARGET", "${TARGET_HW}"),
    ],
)


env.Append(
    CPPPATH=[
        "#/core",
        "#/applications",
        "#/firmware/targets/f${TARGET_HW}/ble_glue",
        "#/firmware/targets/f${TARGET_HW}/fatfs",
        "#/firmware/targets/f${TARGET_HW}/furi_hal",
        "#/firmware/targets/f${TARGET_HW}/Inc",
        "#/firmware/targets/furi_hal_include",
    ]
)

depends = env.BuildModules(
    [
        "lib",
        "firmware",
        "assets",
        "core",
    ],
)

fwenv = env.Clone()
Export("fwenv")

SConscript("sconscfg/firmwareopts.scons")
SConscript("sconscfg/appmanifests.scons")


if fwenv["FIRMWARE_BUILD_CFG"] == "updater":
    fwenv.Append(
        APPS=[
            "updater",
        ]
    )
else:
    fwenv.Append(
        APPS=[
            "crypto_start",
            # Svc
            "basic_services",
            # Apps
            "basic_apps",
            "updater_app",
            "archive",
            # Settings
            "passport",
            "system_settings",
            "about",
            # Plugins
            "basic_plugins",
            # Debug
            "debug_apps",
            "infrared_monitor",
        ]
    )

fwenv.LoadApplicationManifests()
fwenv.PrepareApplicationsBuild()

fwenv.Append(
    CPPDEFINES=fwenv["APPBUILD"].get_apps_cdefs(),
)

Import("build_apps_c", "FlipperAppType")

apps_c = fwenv.Command(
    "applications/applications.c",
    [],
    action=build_apps_c,
)
fwenv.AlwaysBuild(apps_c)


sources = [apps_c]
for app_folder in fwenv["APPBUILD"].get_builtin_app_folders():
    sources += fwenv.GlobRecursive("*.c*", os.path.join("applications", app_folder))


# Building external applications

appenv = fwenv.Clone()
appenv.VariantDir("extapps", "applications", duplicate=False)
appenv.Replace(
    LINKER_SCRIPT="application-ext",
    STRIPFLAGS=[
        "--strip-debug",
        "--strip-unneeded",
        "-d",
        "-g",
        "-S",
    ],
)
appenv.Append(
    CCFLAGS=[
        "-Os",
        "-ggdb3",
        "-mword-relocations",
        "-mlong-calls",
        "-fno-common",
        "-nostdlib",
        "-fvisibility=hidden",
    ],
    LINKFLAGS=[
        "-r",
        "-s",
        # "-Bsymbolic",
        "-nostartfiles",
        "-mlong-calls",
        "-fno-common",
        "-nostdlib",
        "-Wl,--gc-sections",
        "-Wl,--no-export-dynamic",
        "-fvisibility=hidden",
        "-Wl,-e${APP_ENTRY}",
    ],
)

extapps = []
for app in fwenv["APPBUILD"].get_apps_of_type(FlipperAppType.PLUGIN):
    app_elf = appenv.Program(
        os.path.join("extapps", app.appid),
        appenv.GlobRecursive("*.c*", os.path.join("extapps", app._appdir)),
        APP_ENTRY=app.entry_point,
    )
    extapps.append(appenv.ELFStripper(app.appid, app_elf))
Alias("extapps", extapps)


# Git Version management

build_version = fwenv.Command(
    "#lib/toolbox/version.inc.h",
    [],
    Action(
        "${PYTHON3} scripts/version.py generate -o ${TARGET} --dir ${ROOT_DIR}",
        "${VERSIONCOMSTR}",
    ),
)
fwenv.Precious(build_version)
fwenv.AlwaysBuild(build_version)


# Full firmware definition

fwenv.Append(
    LINKFLAGS=[
        "-specs=nano.specs",
        "-specs=nosys.specs",
        "-Wl,--start-group",
        "-lstdc++",
        "-lsupc++",
        "-Wl,--end-group",
        "-Wl,--gc-sections",
        "-Wl,--undefined=uxTopUsedPriority",
        "-Wl,--wrap,_malloc_r",
        "-Wl,--wrap,_free_r",
        "-Wl,--wrap,_calloc_r",
        "-Wl,--wrap,_realloc_r",
        "-u _printf_float",
        "-n",
    ],
)

# Debug
# print(fwenv.Dump())

firmware_elf = fwenv.Program(
    "${FIRMWARE_BUILD_CFG}",
    sources,
    LIBS=[
        "flipper${TARGET_HW}",
        "core",
        "freertos",
        "stm32cubewb",
        "hwdrivers",
        "fatfs",
        "littlefs",
        "subghz",
        "flipperformat",
        "toolbox",
        "microtar",
        "usb_stm32",
        "st25rfal002",
        "infrared",
        "appframe",
        "assets",
        "misc",
        # 2nd round
        "flipperformat",
        "toolbox",
    ],
)

Depends(firmware_elf, depends)


fwenv.HEXBuilder("${FIRMWARE_BUILD_CFG}")
fwenv.BINBuilder("${FIRMWARE_BUILD_CFG}")
dfu = fwenv.DFUBuilder("${FIRMWARE_BUILD_CFG}")
Default(dfu)
Alias("dfu", dfu)


flash = fwenv.Flasher("${FIRMWARE_BUILD_CFG}")
if fwenv["FORCE"]:
    AlwaysBuild(flash)
Alias("flash", flash)


# Compile DB generation
cdb = fwenv.CompilationDatabase("compile_database.json")
Alias("cdb", cdb)
# Depends(firmware_elf, cdb)

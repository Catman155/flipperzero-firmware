Import("env")

from fbt.builders.version import add_version_builders, get_fast_git_version_id


env.Append(
    CPPPATH=[
        "#/lib/toolbox",
    ],
)


libenv = env.Clone(FW_LIB_NAME="toolbox")
libenv.ApplyLibFlags()

add_version_builders(libenv)

# Git Version management
version_depends = []
version_id_data = get_fast_git_version_id()
if version_id_data:
    version_depends = Value(version_id_data)

# Only invoke version generator if preliminary check target (version_depends) has changed
build_version = libenv.VersionBuilder(
    Dir("."),
    version_depends,
)

Default(libenv.InstallAs("${BUILD_DIR}/${FIRMWARE_BUILD_CFG}.json", "version.json"))


if not version_depends:
    libenv.Precious(build_version)
    libenv.AlwaysBuild(build_version)

sources = libenv.GlobRecursive("*.c")

libenv.Append(CPPPATH=["."])

lib = libenv.StaticLibrary("${FW_LIB_NAME}", sources)
libenv.Install("${LIB_DIST_DIR}", lib)
Return("lib")
